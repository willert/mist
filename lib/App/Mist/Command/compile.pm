package App::Mist::Command::compile;
# ABSTRACT: Recompile mpan-install for this project

use 5.010;

use App::Mist -command;

use App::Mist::Utils qw/ append_module_source append_text_file /;
use Module::Path qw/ module_path /;

use Try::Tiny;
use File::Copy;
use File::Share qw/ dist_file /;
use Path::Class qw/ dir /;
use Cwd;

use ExtUtils::Manifest;

sub execute {
  my ( $self, $opt, $args ) = @_;

  my $ctx  = $self->app->ctx;
  my $home = $ctx->project_root;
  my $mpan = $ctx->mpan_dist;

  chdir $home->stringify;


  print "Checking sanity of project files\n";
  if ( -r -f $home->file('MANIFEST.SKIP')->stringify ) {
    my $skipchk = ExtUtils::Manifest::maniskip();
    my @invalid_shebang;
    $home->traverse(sub {
      my ($child, $cont) = @_;
      my $local = $child->relative( $home );
      return if $skipchk->( $local );
      if ( $child->is_dir ) {
        return if $child->basename eq 'perl5'; # don't descent into mist env
        return if $child->basename eq 'node_modules'; # don't descent into npm lib
        return $cont->();
      } else {
        my $fh = eval{ $child->openr }
          or return;
        push @invalid_shebang, $local if <$fh> =~ m{^#.*bin/perl} ;
      }
    });

    print STDERR "WARNING: invalid shebang found (use '#!/usr/bin/env perl' instead):",
      join( qq{\n  }, '', @invalid_shebang ), "\n" if @invalid_shebang;
  } else {
    print STDERR "WARNING: no MANIFEST.SKIP file found in project root\n";
    print STDERR "         skipping sanity scan of project files\n";
  }

  try {

    my $assert  = "\n# TODO: assertions not yet implemented\n";

    my @prepend = $ctx->dist->get_prepended_modules;
    my @notest  = $ctx->dist->get_modules_not_to_test;
    my @prereqs = $ctx->fetch_prereqs;

    my $perl_version = $ctx->perl_version;

    print "Generating mpan-install\n";

    open my $out, ">", "mpan-install.tmp" or die $!;
    print $out "#!/usr/bin/env perl\n\n";

    append_module_source( 'Getopt::Long' => $out, begin_lift => 1 );

    append_module_source(
      'App::cpanminus::fatscript' => $out,
      until => qr/# END OF FATPACK CODE\s*$/,
    );

    append_module_source( 'Getopt::Long::Parser' => $out, begin_lift => 1 );
    append_module_source( 'Devel::CheckBin'      => $out, begin_lift => 1 );
    append_module_source( 'Devel::CheckLib'      => $out, begin_lift => 1 );
    append_module_source( 'Devel::CheckCompiler' => $out, begin_lift => 1 );
    append_module_source( 'Probe::Perl'          => $out, begin_lift => 1 );

    append_module_source( 'Mist::Distribution' => $out );
    append_module_source( 'Mist::Environment'  => $out );

    print $out $ctx->mist_environment->as_code( package => 'DISTRIBUTION' );

    append_text_file(
      dist_file( 'App-Mist', 'cmd-wrapper.bash' ) => $out,
      package => 'CMD_WRAPPER::Bash',
    );

    # has to be included before Mist::Script::install so it has unfettered
    # access to @ARGV
    append_module_source('Mist::Script::perlbrew' => $out, VARS => [
      PERLBREW_ROOT            => $ctx->perlbrew_root,
      PERLBREW_DEFAULT_VERSION => $perl_version,
    ]);

    append_module_source( 'Mist::Script::install' => $out, VARS => [
      MPAN_DIST_DIR      => $mpan->relative( $home ),
      PREREQUISITE_DISTS => \@prereqs,
    ]);

    append_module_source( 'Mist::Script::usage' => $out, verbatim => 1 );

    print $out <<"VERSION_INFO";

=head1 VERSION

 This script was generated by App::Mist version $App::Mist::VERSION

=cut

VERSION_INFO

    close $out;

    unlink "mpan-install";
    rename "mpan-install.tmp", "mpan-install";
    chmod 0755, "mpan-install";

  } catch {

    warn "$_\n";

  } finally {

    unlink "mpan-install.tmp"

  };

}

1;
